plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.2.RELEASE' apply false
}

ext {
    jdk_version = 1.8
    yzlsoft_version = '1.0-SNAPSHOT'
    enableGenerate = false
}

// 只配置结构末梢项目
configure(subprojects.findAll { it.name.startsWith("module-") || it.name.endsWith("-server") }) {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    group 'cn.yzlsoft.unitedfront'
    version '1.0-SNAPSHOT'

    sourceCompatibility = jdk_version


    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    repositories {
        mavenCentral()
        maven {
            url "http://192.168.3.168:8081/repository/maven-snapshots/"
        }
        maven {
            url "http://192.168.3.168:8081/repository/maven-releases/"
        }
    }

    configurations {
        mybatisGenerator
    }

    // 公共依赖
    dependencies {
        mybatisGenerator "org.mybatis.generator:mybatis-generator-core:1.3.7"
        mybatisGenerator 'mysql:mysql-connector-java:5.1.45'
        mybatisGenerator 'cn.yzlsoft.framework:lib-mybatis-generator:' + yzlsoft_version
    }


    if (project.name.startsWith("module-")) {
        task mybatisGenerate {
            doLast {
                println project.name + "-------------" + project.enableGenerate
                if (!project.enableGenerate) {
                    println "尚未启用生成"
                    return
                }
                def targetProject = projectDir.path
                def srcMainJava = sourceSets.main.java.srcDirs[0].path
                def srcMainResources = sourceSets.main.resources.srcDirs[0].path
                ant.properties['targetProject'] = targetProject
                ant.properties['src_main_java'] = srcMainJava
                ant.properties['src_main_resources'] = srcMainResources
                ant.taskdef(
                        name: 'mbgenerator',
                        classname: 'org.mybatis.generator.ant.GeneratorAntTask',
                        classpath: configurations.mybatisGenerator.asPath
                )
                ant.mbgenerator(overwrite: true, configfile: srcMainResources + '/config/generator.xml', verbose: true) {
                    propertyset {
                        propertyref(name: 'targetProject')
                        propertyref(name: 'src_main_java')
                        propertyref(name: 'src_main_resources')
                    }
                }
            }
        }
    }


}

//allprojects {
//    afterEvaluate { project ->
//        println "项目：$project.name"
//    }
//}
//
//gradle.taskGraph.beforeTask { Task task ->
//    println "executing $task ..."
//}
